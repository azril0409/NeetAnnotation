plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'io.github.azril0409'
version = '1.3.2'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

dependencies {
    api project(':Neetannotation-api')
    api fileTree(include: ['*.jar'], dir: 'libs')
    api 'com.google.auto.service:auto-service:1.0'
    api 'com.squareup:javapoet:1.13.0'
    // Lifecycle
    //noinspection LifecycleAnnotationProcessorWithJava8
    api "androidx.lifecycle:lifecycle-compiler:2.4.0"
    // Dagger2
    api "com.google.dagger:dagger:$dagger_version"
    api "com.google.dagger:dagger-android-support:$dagger_version"
    api "com.google.dagger:dagger-compiler:$dagger_version"
    api "com.google.dagger:dagger-android-processor:$dagger_version"
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    useInMemoryPgpKeys(
            properties.getProperty("signing.keyId"),
            properties.getProperty("signing.key"),
            properties.getProperty("signing.password")
    )
    sign publishing.publications
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId group
                artifactId 'NeetAnnotation-compiler'
                version version

                artifact sourcesJar
                artifact javadocJar

                pom {
                    name = 'NeetAnnotation-compiler'
                    description = 'NeetAnnotation is an open source annotation framework for Android development. It base on android lifecycle, dagger2 and Rxjava.'
                    url = 'https://github.com/azril0409/NeetAnnotation'
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            name = 'azril0409'
                            email = 'TU TSUNG-TSE'
                            url = 'azril0409@gmail.com'
                        }
                    }
                    scm {
                        connection = 'https://github.com/azril0409/NeetAnnotation.git'
                        developerConnection = 'https://github.com/azril0409/NeetAnnotation.git'
                        url = 'https://github.com/azril0409/NeetAnnotation'
                    }
                }
            }
        }
        repositories {
            maven {
                //上傳到 sonatype 的 nexus repository manager 代為 push 到 maven central
                name = "sonatype"
                def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                def name = properties.getProperty("ossrhUsername") ?: ''
                def pw = properties.getProperty("ossrhPassword") ?: ''
                credentials {
                    username name
                    password pw
                }
            }
        }
    }
}


def libname = name + "_" + version + ".jar"
def toPath = '../librarys/'
def fromPath = 'build/libs/'
task clearAar(type: Delete) {
    delete toPath + '/' + libname
}
task makeAar(type: Copy) {
    from(fromPath)
    into(toPath)
    include("NeetAnnotation-compiler.jar")
    rename("NeetAnnotation-compiler.jar", libname)
}
makeAar.dependsOn(clearAar, "build")